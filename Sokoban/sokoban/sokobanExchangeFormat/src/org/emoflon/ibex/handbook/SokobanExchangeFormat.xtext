grammar org.emoflon.ibex.handbook.SokobanExchangeFormat

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate sokobanExchangeFormat "http://www.emoflon.org/ibex/handbook/SokobanExchangeFormat"


// Block.................: $ 
// Boulder...............: # 
// Sokoban...............: @ 

// Block on end field....: * 
// Sokoban on end field..: +

// End field.............: . 
// Field.................:   

Board :
	'Name::' name=STRING 
	NEWLINE
	'Author::' author=STRING 
	NEWLINE
	NEWLINE
	firstRow=Row
;

Row :
	firstEntry=Entry (NEWLINE next=Row)?
;

Entry: 
	End | Normal
;

End: {End}
	(END_FIELD | symbol=EndSymbol) (next=Entry)? 
;

Normal: {Normal}
	(FIELD | symbol=NormalSymbol) (next=Entry)?
;

EndSymbol:
	value=EndValue
;

NormalSymbol:
	value=NormalValue
;

Symbol : EndSymbol | NormalSymbol ;

enum EndValue:
	SOKOBAN_ON_END_FIELD = '+' |
	BOX_ON_END_FIELD = '*'
;

enum NormalValue:
	BOULDER = '#' |	
	SOKOBAN = '@' |
	BOX = '$'
;

terminal END_FIELD: '.';
terminal FIELD: ' ';

terminal NEWLINE: ('\r'? '\n');

terminal STRING:
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
		;