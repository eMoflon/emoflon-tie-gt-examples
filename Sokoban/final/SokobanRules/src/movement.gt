import "http://www.eclipse.org/emf/2002/Ecore"
import "platform:/resource/SokobanRules/model/SokobanRules.ecore"

pattern isSokoban {
  figure: Figure

  sokoban: Sokoban:=figure
}

abstract rule moveSokoban {
  from: Field

  to: Field

  sokoban: Sokoban {
    -- -field -> from
    ++ -field -> to
  }
}

rule moveSokobanUp
refines moveSokoban {
  from: Field {
    -top -> to
  }
}

rule moveSokobanDown
refines moveSokoban {
  from: Field {
    -bottom-> to
  }
}

rule moveSokobanRight
refines moveSokoban {
  from: Field {
    -right-> to
  }
}

rule moveSokobanLeft
refines moveSokoban {
  from: Field {
    -left -> to
  }
}
